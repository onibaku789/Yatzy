plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'jacoco-report-aggregation'
    id 'io.gitlab.arturbosch.detekt' version '1.23.0'
}


repositories {
    mavenCentral()
}

group = 'org.example'
version = '1.0-SNAPSHOT'

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // JUnit Jupiter
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.9.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    // AssertJ dependency
    testImplementation 'org.assertj:assertj-core:3.21.0'
}


test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
kotlin {
    jvmToolchain(17)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.800000
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    dependsOn tasks.named('jacocoTestCoverageVerification')
}

detekt {
    ignoreFailures = true // set me false to fail on error
    config.setFrom("$projectDir/config/detekt/detekt.yml")
    buildUponDefaultConfig = false // preconfigure defaults
    allRules = false // activate all available (even unstable) rules.
    reports {
        html.required = true
    }
}


testlogger {
    // pick a theme - mocha, standard, plain, mocha-parallel, standard-parallel or plain-parallel
    theme 'mocha-parallel'

    // set to false to disable detailed failure logs
    showExceptions true

    // set to false to hide stack traces
    showStackTraces true

    // set to true to remove any filtering applied to stack traces
    showFullStackTraces false

    // set to false to hide exception causes
    showCauses true

    // set threshold in milliseconds to highlight slow tests
    slowThreshold 2000

    // displays a breakdown of passes, failures and skips along with total duration
    showSummary true

    // set to true to see simple class names
    showSimpleNames false

    // set to false to hide passed tests
    showPassed true

    // set to false to hide skipped tests
    showSkipped true

    // set to false to hide failed tests
    showFailed true

    // enable to see standard out and error streams inline with the test results
    showStandardStreams false

    // set to false to hide passed standard out and error streams
    showPassedStandardStreams true

    // set to false to hide skipped standard out and error streams
    showSkippedStandardStreams true

    // set to false to hide failed standard out and error streams
    showFailedStandardStreams true
}